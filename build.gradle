// Plugins
plugins {
    id 'net.saliman.properties' version '1.4.6'
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'java-library'

// Project config
group = 'de.unihannover.se.iviewxjava'
version = '0.1.0'

// JVM
sourceCompatibility = 1.8

// Repositories
repositories {
    jcenter()
}

// Dependencies
dependencies {
	// Compile
    compile 'net.java.dev.jna:jna:4.1.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.5'
    compile 'com.google.code.gson:gson:2.8.0'

    // Test
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.awaitility:awaitility:2.0.0'
}

// Maven publication configuration
publishing {
    publications {
        IViewXJavaPublication(MavenPublication) {
            from components.java
            groupId 'de.unihannover.se.iviewxjava'
            artifactId 'iviewx-java'
            version '0.1.0'
        }
    }
}

// Bintray configuration
bintray {
    user = project.property('bintray_user')
    key  = project.property('bintray_key')

    publications = ['IViewXJavaPublication']

    dryRun = false
    publish = true
    override = false

    pkg {
        repo = 'iviewx-java'
        name = 'iviewx-java'
        userOrg = 'fg-se'
        desc = 'Java adapter for the SMI IViewX SDK'
        licenses = ['Apache-2.0']
        labels = ['eyetracking', 'java', 'iviewx']
        vcsUrl = 'https://github.com/FG-SE/iviewx-java.git'
        issueTrackerUrl = 'https://github.com/FG-SE/iviewx-java/issues'
        githubRepo = 'FG-SE/iviewx-java'
        publicDownloadNumbers = true

        version {
            name = '0.1.0'
            desc = 'Testing correct deployment to Bintray'
            released = new Date()
            vcsTag = '0.1.0'
        }
    }
}

// Test configuration
test {
	testLogging.showStandardStreams = true
}

// Tasks
javadoc {
	options.links("https://docs.oracle.com/javase/8/docs/api/")
	exclude "**/iviewxapi/**"
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// Task Grouping

def ARCHIVE_GROUP = 'archives'

jar.group = ARCHIVE_GROUP

fatJar.group = ARCHIVE_GROUP
fatJar.description = 'Assembles a jar archive containing the main classes and all third party dependencies.'